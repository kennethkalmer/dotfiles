#!/usr/bin/env bb

(require '[babashka.cli :as cli]
         '[babashka.http-client :as http]
         '[cheshire.core :as json])

(def cli-options {:minutes {:default 5 :coerce :long}})

(defn tap [x]
  (prn x)
  x)

(defn get-password []
  (let [password (System/getenv "PI_HOLE_API_TOKEN")]

    (if (nil? password)
      (throw (ex-info "PI_HOLE_API_TOKEN is not set" {}))
      password)))

(defn get-sid [password]
  (-> (http/post "http://pi.hole/api/auth"
                 {:headers {"Content-Type" "application/json"}
                  :body (json/generate-string {:password password})})
      :body
      (json/parse-string true)
      (get-in [:session :sid])))

(defn disable-pi-hole [sid minutes]
  (let [url "http://pi.hole/api/dns/blocking"
        body (json/generate-string {:blocking false
                                    :timer (* minutes 60)
                                    :sid sid})
        headers {"Content-Type" "application/json"}]

    (http/post url {:headers headers :body body})))

(let [opts (cli/parse-opts *command-line-args* {:spec cli-options})
      password (get-password)
      sid (get-sid password)
      minutes (:minutes opts)]

  (prn (str "Disabling Pi-hole for " minutes " minutes..."))
  (disable-pi-hole sid (:minutes opts)))